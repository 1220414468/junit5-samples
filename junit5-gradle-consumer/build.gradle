buildscript {
	repositories {
		mavenCentral()
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	}
	dependencies {
		classpath 'org.junit:junit-gradle:5.0.0-SNAPSHOT'
	}
}

repositories {
	mavenCentral()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

ext.junit4Version = '4.12'
ext.junit5Version = '5.0.0-SNAPSHOT'
ext.log4JVersion  = '2.5'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.junit.gen5.gradle'

jar {
	baseName = 'junit5-gradle-consumer'
	version = '1.0.0-SNAPSHOT'
}

compileTestJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	options.compilerArgs += '-parameters'
}

junit5 {
	engines {
		include 'junit5', 'junit4'
	}
	tags {
		// include 'fast'
		exclude 'slow'
	}
	matchClassName '.*Test'
	reportsDir file('build/test-results/junit5/') // this is the default
	logManager 'org.apache.logging.log4j.jul.LogManager'
}

dependencies {
	// JUnit 5 API and TestEngine implementation
	testCompile("org.junit:junit5-api:${junit5Version}")
	testRuntime("org.junit:junit5-engine:${junit5Version}")

	// If you also want to support JUnit 3 and JUnit 4 tests
	testCompile("junit:junit:${junit4Version}")
	testRuntime("org.junit:junit4-engine:${junit5Version}")

	testRuntime("org.apache.logging.log4j:log4j-core:${log4JVersion}")
	testRuntime("org.apache.logging.log4j:log4j-jul:${log4JVersion}")
}

task wrapper(type: Wrapper) {
	distributionUrl = 'https://services.gradle.org/distributions/gradle-2.13-all.zip'
}
