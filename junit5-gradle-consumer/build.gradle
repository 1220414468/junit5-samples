buildscript {
	repositories {
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	}
	dependencies {
		classpath 'org.junit:junit-gradle:5.0.0-SNAPSHOT'
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.junit.gen5.gradle'

jar {
	baseName = 'junit5-gradle-consumer'
	version = '1.0.0-SNAPSHOT'
}

ext.junit4Version = '4.12'
ext.log4JVersion  = '2.5'

junit5 {
	version '5.0.0-SNAPSHOT'
	runJunit4 true
	matchClassName '.*Test'
	// reportFile file('build/test-results/junit5-report.txt') // this is the default
	logManager 'org.apache.logging.log4j.jul.LogManager'
	// includeTag 'fast'
	excludeTag 'slow'
}

repositories {
	mavenCentral()
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

compileTestJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
	options.compilerArgs += '-parameters'
}

dependencies {
	// If you also want JUnit4 tests
	testCompile("junit:junit:${junit4Version}")

	testRuntime("org.apache.logging.log4j:log4j-core:${log4JVersion}")
	testRuntime("org.apache.logging.log4j:log4j-jul:${log4JVersion}")
}

configurations.all {
	// Do NOT cache JUnit 5 snapshot JARs.
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

eclipse {
	classpath {
		containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

task wrapper(type: Wrapper) {
	distributionUrl = 'https://services.gradle.org/distributions/gradle-2.9-all.zip'
}
