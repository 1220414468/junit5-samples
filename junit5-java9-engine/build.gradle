apply plugin: 'java'

repositories {
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	mavenCentral()
}

dependencies {
	// To avoid compiler warnings about @API annotations in JUnit code.
	compileOnly('org.apiguardian:apiguardian-api:1.0.0')

	// We implement `TestEngine` interface.
	compile("org.junit.platform:junit-platform-engine:1.1.0-SNAPSHOT")

	// Needed to run JUnit Platform on the console.
	testRuntime("org.junit.platform:junit-platform-console:1.1.0-SNAPSHOT")
}

//
// Define global properties.
//
ext.moduleName = 'ice.cream'
ext.deps = "$buildDir/deps"

//
// Copy compile- and runtime dependencies to a single directory.
//
task generateDependenciesDirectory(type: Copy) {
	into deps
	from configurations.compileClasspath
	from configurations.testRuntimeClasspath
}

//
// Use module-path and module-version options of javac 9.
//
compileJava {
	options.compilerArgs = [
			'--module-path', classpath.asPath,
			'--module-version', '47.11'
	]
	classpath = files()
}

//
// Execute console launcher on the module-path.
//
task testIceCreamMachine(type: Exec, dependsOn: generateDependenciesDirectory) {
	executable = 'java'
	args = [
			'-Dscoops=' + System.getProperty('scoops', '3'),
			'--module-path', files(deps, compileJava.destinationDir).asPath,
			'--add-modules', moduleName,
			'--module', 'org.junit.platform.console',
			'--scan-class-path'
	]
}

test.dependsOn testIceCreamMachine
